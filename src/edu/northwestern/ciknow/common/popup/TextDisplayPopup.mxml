<?xml version="1.0" encoding="utf-8"?>
<s:TitleWindow xmlns:fx="http://ns.adobe.com/mxml/2009" 
			   xmlns:s="library://ns.adobe.com/flex/spark" 
			   xmlns:mx="library://ns.adobe.com/flex/mx" 
			   width="100%" height="100%" 
			   creationComplete="init()" 
			   close="close()" 
			   backgroundColor="0xffffff"
			   xmlns:parsley="http://www.spicefactory.org/parsley">
	<s:layout>
		<s:VerticalLayout/>
	</s:layout>

	<fx:Declarations>
		<parsley:Configure/>
	</fx:Declarations>
	
	<fx:Script>
		<![CDATA[
			import edu.northwestern.ciknow.common.events.GeneralEvent;
			import edu.northwestern.ciknow.common.util.LogUtil;
			
			import mx.events.CloseEvent;
			import mx.logging.ILogger;
			import mx.managers.PopUpManager;
			
			private static var logger:ILogger = LogUtil.getLog(TextDisplayPopup);
			
			[Bindable]
			public var imagesource:String = null;
			
			[Bindable]
			public var msg:String = "";
			
			[Bindable]
			public var textWidth:Number = 450;
			
			[Bindable]
			public var textHeight:Number = 300;
			
			[Bindable]
			public var textColor:uint = 0x000000;
			
			private function init():void{
				logger.info("initializing...");
				PopUpManager.centerPopUp(this);
			}
			
			public function showCloseButton(value:Boolean):void{
				this.closeButton.visible = value;
				this.closeBtn.enabled = value;
			}
			
			[MessageHandler(selector="popuplateTextDisplayPopup")]
			public function populate(event:GeneralEvent):void{
				var data:Object = event.data;
				if (data.hasOwnProperty("title")){
					this.title = data.title;
				}
				if (data.hasOwnProperty("imagesource")){
					imagesource = data.imagesource;
				}
				if (data.hasOwnProperty("msg")){
					msg = data.msg;
				}
				if (data.hasOwnProperty("textWidth")) {
					textWidth = Number(data.textWidth);
				}
				if (data.hasOwnProperty("textHeight")) {
					textHeight = Number(data.textHeight);
				}
				if (data.hasOwnProperty("textColor")) {
					textColor = uint(data.textColor);
				}
				if (data.hasOwnProperty("showCloseButton")) {
					var showClose:Boolean = Boolean(data.showCloseButton);
					showCloseButton(showClose);
				}
			}
			
			[MessageHandler(selector="gotErrorMsg")]
			public function gotErrorMsg(event:GeneralEvent):void{
				msg = String(event.data);
				textColor = 0xff0000;
			}
			
			private function close():void{
				PopUpManager.removePopUp(this);
			}
		]]>
	</fx:Script>
	
	<s:Image id="logo" source="{imagesource}" includeInLayout="{imagesource != null}" width="50" height="50"/>
	<s:TextArea id="ta" width="{textWidth}" height="{textHeight}" text="{msg}" color="{textColor}" editable="true"/>
	<s:controlBarContent>
		<s:HGroup width="100%" horizontalAlign="right">
			<s:Button id="closeBtn" label="Close" click="close()"/>	
		</s:HGroup>		
	</s:controlBarContent>
</s:TitleWindow>
